/* $NetBSD: bootxx.ldscript,v 1.1.8.2 2006/09/03 15:22:56 yamt Exp $ */
OUTPUT_FORMAT("elf32-tradbigmips")
OUTPUT_ARCH(mips)
STARTUP(entry.o)

/*0xa0190000 : EWS-UX's 1stboot VMA */

MEMORY {
	ram	: o = 0xa0190000, l = 4K
}

SECTIONS {
	. = 0xa0190000;
	.text ALIGN(4) :
	{
		_text = .;
		*(.text)
		_etext = .;
	} > ram
	.reginfo : { *(.reginfo) } > ram
	.data ALIGN(4) :
	{
		_data = .;
		*(.rodata)
		*(.rodata.*)
		*(.data)
		_edata = .;
		PROVIDE (edata = .);
	} > ram
	.bss ALIGN(4) :
	{
		_bss = .;
		*(.bss)
		*(.scommon)
		_ebss = .;
	} > ram
	_end = .;
	PROVIDE (end = .);	/* libsa/alloc.c use this symbol */
}
