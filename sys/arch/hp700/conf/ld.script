/*	$NetBSD: ld.script,v 1.3.6.5 2005/11/10 13:56:10 skrll Exp $	*/

/* 	$OpenBSD: ld.script,v 1.7 1999/12/17 07:02:38 mickey Exp $	*/

OUTPUT_FORMAT("elf32-hppa-netbsd")
OUTPUT_ARCH(hppa)
ENTRY(start)
SECTIONS
{
	/* Read-only sections, merged into text segment: */
	. = + 0x1000; /* XXX should be SIZEOF_HEADERS */
	.text :
	{
		kernel_text = . ;
		*(.text)
		*(.text.*)
		*(.rodata) *(.rodata1)
		*(.reginfo)
		*(.init)
		*(.stub)
		/* .gnu.warning sections are handled specially by elf32.em.  */
		*(.gnu.warning)
		. = ALIGN(8);
		__unwind_start = .;
		*(.PARISC.unwind)
		__unwind_end = .;
		. = ALIGN(4096);
	} = 0 /* 0x08000240 nop filled, does not work */
	etext = ABSOLUTE(.);
	. = ALIGN(0x200000);

	.data  :
	{
		$global$ = . ;
		. += 4;
		*(.data)
		/* [a-ps-z]*(.data) r[a-ce-z]*(.data) *libkern.o(.data) */
		/* rd_root.o(.data)	* there is an overflow bug in ld */
		CONSTRUCTORS
		. = ALIGN(4096);
		edata = ABSOLUTE(.);
	} = 0
	.ctors	: { *(.ctors) }
	.dtors	: { *(.dtors) }

	__bss_start = .;
	/* .sbss	: { *(.sbss) *(.scommon) } */
	.bss :
	{
		*(.dynbss) *(.bss)
		*(COMMON) *($COMMON$)
		. = ALIGN(4096);
	}
	end = ABSOLUTE(.);
}

